buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }

    }
    dependencies {
        classpath 'org.owasp:dependency-check-gradle:9.0.9'

    }
}


// 3rd party plugin repositories can be configured in settings.gradle
plugins {
    id "io.spring.dependency-management" version "1.1.4"
    id "org.sonarqube" version "5.0.0.4638"
    id 'jacoco'
    id "io.freefair.lombok" version "8.6"
    id "io.freefair.git-version" version "8.6"
    id "com.github.ben-manes.versions" version "0.51.0"
}


// Apply the java plugin to add support for Java
apply plugin: 'org.owasp.dependencycheck'
apply plugin: 'io.freefair.git-version'


allprojects {
    apply plugin: 'java'
    apply plugin: "jacoco"
    apply plugin: 'signing'


    group = 'org.anasoid.jmc'
    java {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_17
    }

    repositories {
        mavenCentral()
        if (rootProject.hasProperty('jmeterSnapshot')) {
            maven { url "https://repository.apache.org/content/repositories/snapshots/" }
        }
    }

    jacoco {
        toolVersion = '0.8.11'
    }


    ext.gradleScriptDir = "${rootProject.projectDir}/gradle"
    apply from: "${gradleScriptDir}/ide.gradle"


}


subprojects {

    apply plugin: 'checkstyle'
    apply plugin: 'pmd'
    apply plugin: "jacoco"
    apply plugin: 'signing'


    System.setProperty("org.checkstyle.google.suppressionfilter.config", rootProject.projectDir.toString() + "/config/checkstyle/checkstyle-suppressions.xml")
    checkstyle {

        configFile = rootProject.file('config/checkstyle/google_checks.xml')
        toolVersion "10.14.2"
        ignoreFailures = false
        maxErrors = 0
        maxWarnings = 0

    }
    pmd {
        toolVersion = "6.47.0"
        consoleOutput = true
        sourceSets = [sourceSets.main]
        ruleSetFiles = files("${rootProject.projectDir}/config/pmd/pmd.xml")
        ruleSets = []
    }

    jacocoTestReport {

        reports {
            html.required = true
            xml.required = false
            csv.required = false
        }
        onlyIf = {
            true
        }

    }
    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
        systemProperty "p", "p1"
        // listen to events in the test execution lifecycle
        beforeTest { descriptor ->
            logger.lifecycle("Running test: " + descriptor)
        }
    }
    tasks.whenTaskAdded { task ->
        if (task.name.equals("distZip") || task.name.equals("distTar")) {
            task.enabled = false
        }
    }

    dependencies {

        // SLF4J as a facade over Log4j2 required dependencies
        implementation("org.apache.logging.log4j:log4j-slf4j-impl:$log4j2Version")


        //JMETER
        implementation("org.apache.jmeter:ApacheJMeter_java:$jmeterVersion") {
            exclude group: 'org.apache.jmeter', module: 'bom'
        }
        implementation("org.apache.jmeter:ApacheJMeter_http:$jmeterVersion") {
            exclude group: 'org.apache.jmeter', module: 'bom'
        }
        implementation("org.apache.jmeter:ApacheJMeter_config:$jmeterVersion") {
            exclude group: 'org.apache.jmeter', module: 'bom'
        }

        implementation("com.thoughtworks.xstream:xstream:$xstreamVersion")

        //LOMBOCK
        compileOnly "org.projectlombok:lombok:$lombockVersion"
        annotationProcessor "org.projectlombok:lombok:$lombockVersion"

        testCompileOnly "org.projectlombok:lombok:$lombockVersion"
        testAnnotationProcessor "org.projectlombok:lombok:$lombockVersion"


        //Apache common

        implementation("org.apache.commons:commons-lang3:$commonsLangVersion")
        implementation("commons-io:commons-io:$commonsIoVersion")


        //Test
        testImplementation "org.junit.jupiter:junit-jupiter:$junitVersion"
        testRuntimeOnly "org.junit.platform:junit-platform-launcher"
        testImplementation("commons-io:commons-io:$commonsIoVersion")
        testImplementation("org.assertj:assertj-core:$assertjCore")


        testImplementation("org.xmlunit:xmlunit-core:$xmlunitVersion")
        testImplementation("org.xmlunit:xmlunit-matchers:$xmlunitVersion")
        testImplementation("com.github.tomakehurst:wiremock-jre8:$wiremockVersion")
        testImplementation("ru.lanwen.wiremock:wiremock-junit5:$wiremockJunit5Version")
        testImplementation("org.mockito:mockito-core:$mockitoVersion")
        testImplementation("org.mockito:mockito-inline:$mockitoVersion")
        testImplementation("uk.org.webcompere:system-stubs-jupiter:$webcompereVersion")

    }


}

apply from: "${rootProject.projectDir}/gradle/release.gradle"
apply from: "${rootProject.projectDir}/gradle/javadoc.gradle"

task codeCoverageReport(type: JacocoReport) {

    // Gather execution data from all subprojects
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    // Add all relevant sourcesets from the subprojects
    subprojects.each {
        sourceSets it.sourceSets.main
    }
    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }
}


tasks.named("dependencyUpdates").configure {
    checkConstraints = true
    outputDir = "build/reports/dependencyUpdates"
}

// always run the tests before generating the report
codeCoverageReport.dependsOn {
    subprojects*.test
    subprojects*.delombok
}


sonar {
    properties {
        property "sonar.coverage.jacoco.xmlReportPaths", "${rootDir}/build/reports/jacoco/codeCoverageReport/codeCoverageReport.xml"
    }
}

ext {
    linkHomepage = 'https://github.com/anasoid/jmeter-as-code'
    linkIssue = 'https://github.com/anasoid/jmeter-as-code/issues'
    linkScmUrl = 'https://github.com/anasoid/jmeter-as-code'


}







